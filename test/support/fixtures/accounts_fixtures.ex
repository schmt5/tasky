defmodule Tasky.AccountsFixtures do
  @moduledoc """
  This module defines test helpers for creating
  entities via the `Tasky.Accounts` context.
  """

  import Ecto.Query

  alias Tasky.Accounts
  alias Tasky.Accounts.Scope

  def unique_user_email, do: "user#{System.unique_integer()}@example.com"

  def valid_user_attributes(attrs \\ %{}) do
    Enum.into(attrs, %{
      email: unique_user_email(),
      firstname: "Test",
      lastname: "User",
      password: "hello world!"
    })
  end

  def unconfirmed_user_fixture(attrs \\ %{}) do
    {:ok, user} =
      attrs
      |> valid_user_attributes()
      |> Accounts.register_user()

    user
  end

  def user_fixture(attrs \\ %{}) do
    {role, attrs} = Map.pop(attrs, :role)

    {:ok, user} =
      attrs
      |> valid_user_attributes()
      |> Accounts.register_user()

    # Apply role after registration since registration_changeset derives role
    # from :is_teacher and does not cast :role directly.
    user =
      if role do
        {:ok, updated} = Accounts.update_user_role(user, %{role: role})
        updated
      else
        user
      end

    user
  end

  def user_scope_fixture do
    user = user_fixture()
    user_scope_fixture(user)
  end

  def user_scope_fixture(user) do
    Scope.for_user(user)
  end

  def extract_user_token(fun) do
    {:ok, captured_email} = fun.(&"[TOKEN]#{&1}[TOKEN]")
    [_, token | _] = String.split(captured_email.text_body, "[TOKEN]")
    token
  end

  def override_token_authenticated_at(token, authenticated_at) when is_binary(token) do
    Tasky.Repo.update_all(
      from(t in Accounts.UserToken,
        where: t.token == ^token
      ),
      set: [authenticated_at: authenticated_at]
    )
  end

  def offset_user_token(token, amount_to_add, unit) do
    dt = DateTime.add(DateTime.utc_now(:second), amount_to_add, unit)

    Tasky.Repo.update_all(
      from(ut in Accounts.UserToken, where: ut.token == ^token),
      set: [inserted_at: dt, authenticated_at: dt]
    )
  end
end
